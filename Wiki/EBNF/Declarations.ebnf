Module = [Statements];

TargetDefinition = "set_target" AttributeList ";";
Definition       = ["in" | "out"]
                   (BaseType | "void" | "auto")
                   [FP_Cast] [AttributeList] IdentifierList;

BaseType = "pin"  | "sig" | "clk" 
         | "int"  | "rat" | "float" | "complex"
         | "func" ;

IdentifierList = Identifier {ArrayDefinition} (
                  ("(" [DefParameterList] ")" "{" [Statements] "}"   ) |
                  (
                   [Initialiser] 
                   {"," Identifier {ArrayDefinition} [Initialiser]} ";"
                  )
                 );
ArrayDefinition  = "[" [Expression] "]";
Initialiser      = ("=" | ":=") Expression;

DefParameterList = DefParameter {"," DefParameter};
DefParameter     = [BaseType | (Identifier {"." Identifier})] 
                   Identifier {"[" "]"};

AttributeList       = "<" AttributeAssignment {, AttributeAssignment} ">";
AttributeAssignment = Identifier "=" String;

ClassDefinition = "class" [AttributeList] Identifier {"." Identifier}
                  ["("[DefParameterList]")"] 
                  [
                    ":" Identifier ["(" [ParameterList] ")"] 
                   {"," Identifier ["(" [ParameterList] ")"]}
                  ]
                  "{" [Statements] "}";
(*----------------------------------------------------------------------------*)

